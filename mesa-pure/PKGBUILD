# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
## Maintainers of original Arch Linux package:
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=mesa-pure
pkgver=25.2.3
pkgrel=1
pkgdesc="A purely personal Mesa build"
url="https://www.mesa3d.org/"
arch=(x86_64)
license=("MIT AND BSD-3-Clause AND SGI-B-2.0")
makedepends=(
  'cbindgen'
  'cmake'
  'elfutils'
  'glslang'
  'libpng'
  'libva'
  'libxml2'
  'libxrandr'
  'meson'
  'ninja'
  'python-mako'
  'python-packaging'
  'python-ply'
  'python-pyaml'
  'valgrind'
  'wayland-protocols'
  'xorgproto'
)
depends=(
  'expat'
  'gcc-libs'
  'glibc'
  'libdrm'
  'libelf'
  'libglvnd'
  'libunwind'
  'libx11'
  'libxcb'
  'libxdamage'
  'libxext'
  'libxfixes'
  'libxshmfence'
  'libxxf86vm'
  'lm_sensors'
  'python'
  'systemd-libs'
  'vulkan-icd-loader'
  'wayland'
  'xcb-util-keysyms'
  'zlib'
  'zstd'
)
optdepends=('opengl-man-pages: for the OpenGL API man pages')
provides=(
  'libva-mesa-driver'
  'mesa'
  'mesa-libgl'
  'opencl-driver'
  'opengl-driver'
  'vulkan-driver'
  'vulkan-mesa-device-select'
  'vulkan-mesa-layers'
  'vulkan-radeon'
)
conflicts=(
  'libva-mesa-driver'
  'mesa'
  'mesa-libgl'
  'opencl-clover-mesa'
  'vulkan-mesa-device-select'
  'vulkan-mesa-layers'
  'vulkan-radeon'
)
source=(
  "https://mesa.freedesktop.org/archive/mesa-$pkgver.tar.xz"
  anti-lag.patch
)
sha256sums=(
  'f2d6b28562f1d6cb9c17ee8e58eeade7aa5faf927ae71065eadb41e17f92b4f8'
  '1e83a132618ebc2a2236779223ace94354f22b3372ce739ee6641e83f2d0f9e8'
)

prepare() {
  cd "mesa-$pkgver"

  # Include package release in version string so Chromium invalidates
  # its GPU cache; otherwise it can cause pages to render incorrectly.
  # https://bugs.launchpad.net/ubuntu/+source/chromium-browser/+bug/2020604
  echo "$pkgver-pure.$pkgrel" >VERSION

  # Add support for VK_AMD_anti_lag vulkan layer
  patch -Np1 -i "${srcdir}/anti-lag.patch"
}

build() {
  local -a split=($CFLAGS)
  local -A flags
  for opt in "${split[@]}"; do flags["${opt%%=*}"]="${opt##*=}"; done
  local march="${flags["-march"]:-"x86-64-v3"}"

  local meson_options=(
    -D b_lto=true
    -D c_args="-O3 -march=$march"
    -D c_link_args="-O1"
    -D cpp_args="-O3 -march=$march"
    -D cpp_link_args="-O1"
    -D llvm=disabled
    -D amd-use-llvm=false
    -D android-libbacktrace=disabled
    -D b_ndebug=true
    -D gallium-mediafoundation=disabled
    -D gallium-drivers=radeonsi,zink
    -D gallium-extra-hud=false
    -D gallium-vdpau=disabled
    -D gles1=disabled
    -D glx=dri
    -D html-docs=disabled
    -D intel-rt=disabled
    -D libunwind=disabled
    -D microsoft-clc=disabled
    -D platforms=x11,wayland
    -D valgrind=enabled
    -D video-codecs=all
    -D vulkan-drivers=amd
    -D vulkan-layers=device-select,overlay,screenshot,vram-report-limit,anti-lag
  )

  # Build only minimal debug info to reduce size
  CFLAGS+=" -g1"
  CXXFLAGS+=" -g1"

  # Inject subproject packages
  export MESON_PACKAGE_CACHE_DIR="$srcdir"

  arch-meson mesa-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  # indirect rendering
  ln -s /usr/lib/libGLX_mesa.so.0 "${pkgdir}/usr/lib/libGLX_indirect.so.0"

  install -Dm644 mesa-$pkgver/docs/license.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
