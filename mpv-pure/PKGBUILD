# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
## Arch Linux maintainers:
# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-pure
pkgver=0.40.0
pkgrel=3
pkgdesc='a free, open source, and cross-platform media player'
arch=('x86_64')
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
url='https://mpv.io/'
depends=('desktop-file-utils' 'ffmpeg' 'glibc' 'hicolor-icon-theme'
  'lcms2' 'libarchive' 'libass' 'libdisplay-info' 'libdrm' 'libegl' 'libgl' 'libglvnd'
  'libjpeg-turbo' 'libpipewire' 'libplacebo' 'libva' 'libxkbcommon'
  'luajit' 'mesa' 'uchardet' 'vulkan-icd-loader' 'wayland' 'zimg' 'zlib')
makedepends=('ffnvcodec-headers' 'git' 'ladspa' 'meson'
  'python-docutils' 'vulkan-headers' 'wayland-protocols')
optdepends=('yt-dlp: for video-sharing websites playback')
conflicts=('libmpv.so' 'mpv')
provides=('libmpv.so' 'mpv')
options=('!emptydirs')
source=("https://github.com/mpv-player/mpv/archive/v${pkgver}/mpv-${pkgver}.tar.gz"
        "dynamically_generate_desktop_file_protocols.patch")
sha256sums=('10a0f4654f62140a6dd4d380dcf0bbdbdcf6e697556863dc499c296182f081a3'
            '88acf97cbc8e0fe745f09bd0bd7f65e0437adcb549dadf3588fd0724d01298e9')

build() {
  local _meson_options=(
    --buildtype=release
    --auto-features=disabled
    -Dstrip=true
    -Db_lto=true
    -Db_lto_mode=default

    -Dbuild-date=false

    -Dcplugins=enabled
    -Diconv=enabled
    -Djpeg=enabled
    -Dlcms2=enabled
    -Dlibarchive=enabled
    -Dlibavdevice=enabled
    -Dlua=luajit
    -Duchardet=enabled
    -Dvector=enabled
    -Dzimg=enabled
    -Dzlib=enabled

    -Dpipewire=enabled

    -Ddmabuf-wayland=enabled
    -Ddrm=enabled
    -Degl=enabled
    -Degl-drm=enabled
    -Degl-wayland=enabled
    -Dgbm=enabled
    -Dgl=enabled
    -Dsixel=enabled
    -Dvaapi=enabled
    -Dvaapi-drm=enabled
    -Dvaapi-wayland=enabled
    -Dvulkan=enabled
    -Dwayland=enabled

    -Dmanpage-build=enabled
  )

  arch-meson "mpv-${pkgver}" build "${_meson_options[@]}"

  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"

  # delete private entries only required for static linking
  sed -i -e '/Requires.private/d' -e '/Libs.private/d' "${pkgdir}"/usr/lib/pkgconfig/mpv.pc

  install -m0644 "mpv-${pkgver}"/DOCS/{encoding.rst,tech-overview.txt} \
    "${pkgdir}"/usr/share/doc/mpv

  install -m0644 "mpv-${pkgver}"/TOOLS/{umpv,mpv_identify.sh,stats-conv.py,idet.sh,lua/*} \
    -D -t "${pkgdir}"/usr/share/mpv/scripts
}
