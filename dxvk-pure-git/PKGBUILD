# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
## AUR package maintainer:
# Maintainer: Adrià Cereto i Massagué <ssorgatem at gmail.com>

pkgname=dxvk-pure-git
pkgver=2.5.1.r28.gb1ad4314
pkgrel=1
pkgdesc="A Vulkan-based compatibility layer for Direct3D 9/10/11 which allows running 3D applications on Linux using Wine. Windows DLL version)"
arch=('x86_64')
url="https://github.com/doitsujin/dxvk"
license=('zlib/libpng')
depends=('vulkan-icd-loader')
provides=('dxvk' 'd9vk' "dxvk=$pkgver")
conflicts=('d9vk'  "dxvk" 'dxvk-bin')
makedepends=('ninja' 'meson>=0.43' 'glslang' 'mingw-w64-gcc-pure' 'git')
options=(!strip !buildflags staticlibs)
source=(
    "git+https://github.com/doitsujin/dxvk.git"
    "git+https://github.com/Joshua-Ashton/mingw-directx-headers.git"
    "git+https://github.com/KhronosGroup/Vulkan-Headers.git"
    "git+https://github.com/KhronosGroup/SPIRV-Headers.git"
    "dxvk-extraopts.patch"
)
sha256sums=(
    "SKIP"
    "SKIP"
    "SKIP"
    "SKIP"
    "bcc15521e4c7f966a0192a1dabb7fb4935b33db39344ab5b861f9d81486f1362"
)


pkgver() {
    cd dxvk
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
    cd dxvk

    git remote set-url origin https://github.com/doitsujin/dxvk.git
    git submodule update --init --filter=tree:0 --recursive

    # Patch to passthrough compiler flags (thanks @loathingkernel)
    patch -p1 -i "$srcdir"/dxvk-extraopts.patch

    local cross_cflags="-march=native -mtune=native -mprefer-avx128 -mno-align-vector-insn -fwrapv -fno-strict-aliasing -pipe"
    local cross_cxxflags="$cross_cflags -std=c++17"
    local cross_ldflags="-Wl,-O1,--sort-common,--as-needed -Wl,--file-alignment,4096"

    sed -i build-win64.txt \
        -e "s|@CARGS@|\'${cross_cflags// /\',\'}\'|g" \
        -e "s|@CXXARGS@|\'${cross_cxxflags// /\',\'}\'|g" \
        -e "s|@LDARGS@|\'${cross_ldflags// /\',\'}\'|g"

    sed -i build-win32.txt \
        -e "s|@CARGS@|\'${cross_cflags// /\',\'}\'|g" \
        -e "s|@CXXARGS@|\'${cross_cxxflags// /\',\'}\'|g" \
        -e "s|@LDARGS@|\'${cross_ldflags// /\',\'}\'|g"
}

build() {
    meson setup dxvk "build/x64" \
        --cross-file dxvk/build-win64.txt \
        --prefix "/usr/share/dxvk/x64" \
        --bindir "" --libdir "" \
        --buildtype "release" \
        --strip
    ninja -C "build/x64" -v

    meson setup dxvk "build/x32" \
        --cross-file dxvk/build-win32.txt \
        --prefix "/usr/share/dxvk/x32" \
        --bindir "" --libdir "" \
        --buildtype "release" \
        --strip
    ninja -C "build/x32" -v
}

package() {
     DESTDIR="$pkgdir" ninja -C "build/x32" install
     DESTDIR="$pkgdir" ninja -C "build/x64" install
     install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" dxvk/LICENSE
}
