# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
## AUR maintainer:
# Maintainer: twa022 <twa022 at gmail dot com>

_pkgname=gimp
pkgname=${_pkgname}-pure
pkgver=3.0.2
pkgrel=1
pkgdesc="GNU Image Manipulation Program (Development version)"
arch=('x86_64')
url="https://www.gimp.org/"
license=('GPL-3.0-or-later')
depends=('appstream-glib' 'babl>=0.1.110' 'cairo' 'desktop-file-utils' 'enchant'
         'gegl>=0.4.54' 'graphviz' 'gtk3' 'icu' 'lcms2' 'libexif' 'libgexiv2'
         'libgudev' 'librsvg' 'libxmu' 'mypaint-brushes1' 'openexr'
         'openjpeg2' 'poppler-data' 'poppler-glib' 'python-gobject')
makedepends=('aalib' 'appstream' 'cfitsio' 'curl' 'ghostscript'
             'gi-docgen' 'glib2-devel' 'glib-networking' 'gobject-introspection'
             'highway' 'intltool' 'iso-codes' 'libheif' 'libjxl' 'libxslt'
             'libmng' 'libwebp' 'libxpm' 'luajit' 'meson' 'zlib')
optdepends=('aalib: ASCII art support'
            'cfitsio: FITS support'
            'curl: for URI support'
            'ghostscript: for postscript support'
            'iso-codes: Language support'
            'libheif: HEIF support'
            'libilbm: ILBM support'
            'libjxl: JPEG XL support'
            'libmng: MNG support'
            'libwebp: WebP support'
            'libxpm: XPM support'
            'lua51-lgi: LUA scripting support'
            'luajit: LUA scripting support'
            'qoi-headers: QOI image support'
            'zlib: Compression routines')
# 'gutenprint: for sophisticated printing only as gimp has built-in cups print support' # GIMP 2.0 only
conflicts=("${_pkgname}")
provides=("${_pkgname}=${pkgver}")
source=("https://download.gimp.org/gimp/v${pkgver%.*}/${_pkgname}-${pkgver/rc/-RC}.tar.xz"
        'linux.gpl')
sha256sums=('546ddc30cb2d0e79123c7fcb4d78211e1ee7a6aace91a6a0ad8cbcbf6ea571a2'
            '1003bbf5fc292d0d63be44562f46506f7b2ca5729770da9d38d3bb2e8a2f36b3')

build() {
  local meson_options=(
    -Dheadless-tests=disabled
    -Dlua=true
    -Dxcursor=disabled
    -Dwmf=disabled
    -Dvala=disabled
    -Dilbm=disabled
    -Dgi-docgen=disabled
    -Dalsa=disabled
    -Djavascript=disabled
  )

  arch-meson "${_pkgname}-${pkgver/rc/-RC}" build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"

  install -Dm 644 "${srcdir}"/linux.gpl "${pkgdir}/usr/share/gimp/${pkgver%.*}/palettes/Linux.gpl"
}
