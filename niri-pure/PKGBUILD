# Maintainer: Vasiliy Stelmachenok <ventureo@cachyos.org
## Arch Linux's maintainers:
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=niri-pure
pkgver=25.08
pkgrel=1
pkgdesc="A scrollable-tiling Wayland compositor (patched version)"
arch=(x86_64)
url="https://github.com/YaLTeR/niri"
license=(GPL-3.0-or-later)
depends=(
  cairo
  gcc-libs
  glib2
  glibc
  libdisplay-info
  libinput
  libpipewire
  libxkbcommon
  mesa
  pango
  pixman
  seatd
  systemd-libs
  xdg-desktop-portal-impl
)
makedepends=(
  clang
  git
  rust
)
optdepends=(
  'alacritty: a suggested GPU-accelerated terminal emulator'
  'bash: for niri-session script'
  'fuzzel: a suggested Wayland application launcher'
  'mako: a suggested Wayland notification daemon'
  'org.freedesktop.secrets: for apps to rely on secrets portal'
  'swaybg: a suggested Wayland wallpaper tool'
  'swaylock: a suggested Wayland screen locker'
  'waybar: a suggested Wayland customizable desktop bar'
  'xwayland-satellite: for running X11 apps in XWayland'
  'xdg-desktop-portal-gtk: a suggested XDG desktop portal'
  'xdg-desktop-portal-gnome: a XDG desktop portal required for screencasting'
)
conflicts=(niri)
provides=(niri wayland-compositor)
source=(
    "$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
    "wayland-rs::git+https://github.com/KosmX/wayland-rs#commit=e5655d3731daacad6489ebaf79f03644dcaaf5bd"
    update_to_libdisplay-info_3.patch
    Fix-buffer-overflow.patch
)
sha512sums=(
  'd8a10bb726d2e79f695544130cc9f55b1ac0f76dd9a9fb1cafb16cd7934b29a4fecf88656a3bc46ab6140aef7d2c58ed87f3ba43dfe8882df50de997283f2292'
  'f0b554f0de3d083642d3b90c70a16dfab3df16f0addf8fbf077f1de9e4301a4794ef4c533f4042513abe3e3ebedfc2a727f1859fc743697f6965ec87612cdbbe'
  '99aa04588b4d12bbbe3b0d529498c39b26c4b7122c6cb02b355e28e8502c74fd93fa7ef3adc93a8f69a2e0d2e3fb6651d0cce6f3a3e2e8db6117711ee62485f9'
  'dabbcb602e1db4a885b779acf54889608e9b391a0c6fd8ef46d043beb827249f42698a6eb432d1d90b9c66ade7fcc87f445fbbc972616bf981f77f84cdcc2bf2'
)
b2sums=(
  '39758d4ba4ff721d71a116cba0b8cdcd9e1f0a024257885879b5dc31bf439e91109133cc1650f3bd2376a6f8437fb07b4ee137b0b4d8ace98a1397c6b64d74ea'
  '9da41b9df2d28b398812c71453076b9f82101af853b719fb08d71662bfa187b49b1dd264a18bedd5d6a5b33bd5446e650a26daa6f4dfa2803be70d25fd9d33df'
  '3df7266232b0da713c38546a256ffc6dae6801e8d652af642a9c6ab8dc9649bf0710e75a06ff98ad3449da95fdb9d2d665cf2e82a5783c03b2c26bb10d56c514'
  'e17427167ea0a950d7b87808e2e7b27178ef9197d6da6ae403e7fcf6ff99338b12c376102712b966998e580f5fc9ecda8ae48cf29325e886b46ba1b0f2fa1dfb'
)

prepare() {
  cd "niri-$pkgver"

  # Update dependencies to libdisplay-info/libdisplay-info-sys to v0.3.0
  patch -Np1 -i "$srcdir/update_to_libdisplay-info_3.patch"

  # Fixes:
  # https://github.com/YaLTeR/niri/issues/1989
  # https://github.com/YaLTeR/niri/issues/2437
  patch -Np1 -i "$srcdir/Fix-buffer-overflow.patch"

  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "niri-$pkgver"

  export NIRI_BUILD_COMMIT="$(zcat ../$pkgname-$pkgver.tar.gz | git get-tar-commit-id | cut -c1-7)"
  CFLAGS+=(' -ffat-lto-objects')
  cargo build --frozen --release --features default

  # generate shell completions
  for shell in bash fish zsh; do
    cargo run --frozen --release --bin niri -- \
      completions "$shell" > "$shell-completions"
  done
}

check() {
  cd "niri-$pkgver"

  export XDG_RUNTIME_DIR="$(mktemp -d)"
  export RAYON_NUM_THREADS=1  # required so we can build in environments with _many_ threads
  cargo test --all --exclude niri-visual-tests --frozen
}

package() {
  cd "niri-$pkgver"

  install -vDm 755 {target/release/niri,resources/niri-session} -t "$pkgdir/usr/bin/"
  install -vDm 644 resources/niri{.service,-shutdown.target} -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 resources/niri.desktop -t "$pkgdir/usr/share/wayland-sessions/"
  install -vDm 644 resources/niri-portals.conf -t "$pkgdir/usr/share/xdg-desktop-portal/"
  install -vDm 644 resources/default-config.kdl README.md -t "$pkgdir/usr/share/doc/$pkgname/"

  # shell auto-completions
  install -vDm 644 bash-completions "$pkgdir/usr/share/bash-completion/completions/niri"
  install -vDm 644 fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/niri.fish"
  install -vDm 644 zsh-completions "$pkgdir/usr/share/zsh/site-functions/_niri"
}
