# Maintainer: Vasiliy Stelmachenok <ventureo@cachyos.org>
## AUR package maintainer:
# Maintainer: Adrià Cereto i Massagué <ssorgatem at gmail.com>

pkgname=dxvk-pure-git
pkgver=2.7.1.r355.gb650faef2
pkgrel=2
pkgdesc="A Vulkan-based compatibility layer for Direct3D 9/10/11 which allows running 3D applications on Linux using Wine. Windows DLL version)"
arch=('x86_64')
url="https://github.com/doitsujin/dxvk"
license=('zlib/libpng')
depends=('vulkan-icd-loader')
provides=('dxvk' 'd9vk' "dxvk=$pkgver")
conflicts=('d9vk'  "dxvk" 'dxvk-bin')
makedepends=('ninja' 'meson>=0.43' 'glslang' 'clang' 'lld' 'llvm' 'git')
options=(!strip !buildflags staticlibs)
source=(
    "git+https://github.com/doitsujin/dxvk.git"
    "git+https://github.com/Joshua-Ashton/mingw-directx-headers.git"
    "git+https://github.com/KhronosGroup/Vulkan-Headers.git"
    "git+https://github.com/KhronosGroup/SPIRV-Headers.git"
    "dxvk-extraopts.patch"
    "5367.patch"
    "Allow-build-with-AVX-when-using-Clang.patch"
)
sha256sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    '83b240fdfff44b27c80b357055f405233c98e763310c2d1a001cd1b4d51d3aed'
    '4a12f6ced1bbcd05eee0a85e364e1730db8b1da543f4a049c6a3f85aac9fe561'
    'b2ad28c672da301ed8d90b36a37c71f722438e3daa565bcce9115306a1d37016'
)

pkgver() {
    cd dxvk
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
    cd dxvk

    git remote set-url origin https://github.com/doitsujin/dxvk.git
    git submodule update --init --filter=tree:0 --recursive

    # Use Clang to build DXVK
    patch -p1 -i "$srcdir"/5367.patch
    patch -p1 -i "$srcdir"/Allow-build-with-AVX-when-using-Clang.patch

    # Patch to passthrough compiler flags (thanks @loathingkernel)
    patch -p1 -i "$srcdir"/dxvk-extraopts.patch

    local -a split=($CFLAGS)
    local -A flags
    for opt in "${split[@]}"; do flags["${opt%%=*}"]="${opt##*=}"; done
    local march="${flags["-march"]:-"x86-64-v3"}"
    local mtune="${flags["-mtune"]:-"haswell"}"

    export CFLAGS="-march=$march -mtune=$mtune -mfpmath=sse -fwrapv -fno-strict-aliasing -pipe"
    export CXXFLAGS="$CFLAGS -std=c++17"
    export LDFLAGS="-Wl,-O1,--sort-common,--as-needed -Wl,--file-alignment,4096"

    local cross_cflags="$CFLAGS -mcmodel=small"
    local cross_cxxflags="$CXXFLAGS -mcmodel=small"
    local cross_ldflags="$LDFLAGS"

    sed -i build-win64-clang.txt \
        -e "s|@CARGS@|\'${cross_cflags// /\',\'}\'|g" \
        -e "s|@CXXARGS@|\'${cross_cxxflags// /\',\'}\'|g" \
        -e "s|@LDARGS@|\'${cross_ldflags// /\',\'}\'|g"

    local cross_cflags="$CFLAGS"
    local cross_cxxflags="$CXXFLAGS"
    sed -i build-win32-clang.txt \
        -e "s|@CARGS@|\'${cross_cflags// /\',\'}\'|g" \
        -e "s|@CXXARGS@|\'${cross_cxxflags// /\',\'}\'|g" \
        -e "s|@LDARGS@|\'${cross_ldflags// /\',\'}\'|g"
}

build() {
    meson setup dxvk "build/x64" \
        --cross-file dxvk/build-win64-clang.txt \
        --prefix "/usr/share/dxvk/x64" \
        --bindir "" --libdir "" \
        --buildtype "release" \
        --strip
    ninja -C "build/x64" -v

    meson setup dxvk "build/x32" \
        --cross-file dxvk/build-win32-clang.txt \
        --prefix "/usr/share/dxvk/x32" \
        --bindir "" --libdir "" \
        --buildtype "release" \
        --strip
    ninja -C "build/x32" -v
}

package() {
     DESTDIR="$pkgdir" ninja -C "build/x32" install
     DESTDIR="$pkgdir" ninja -C "build/x64" install
     install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" dxvk/LICENSE
}
